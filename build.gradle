apply plugin: 'java'
// For buildscript -> repositories -> maven
apply plugin: 'maven'
// Add automated release process
apply plugin: 'net.researchgate.release'

sourceCompatibility = 1.7
targetCompatibility = 1.7

// Load the version number. This file sits in resources to include it into the jar. Because the version
// is needed at runtime.

// Configure the maven repository deployment
install {
    repositories.mavenInstaller {
        // Set the version
        pom.version = version
        // Set the group/namespace for the maven repository deployment.
        pom.groupId = 'org.hibernate'
    }
}

defaultTasks 'jar'

repositories {
    if (project.hasProperty('local')) {
        mavenLocal()
    }
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven { url 'http://repository.activeeon.com/content/groups/proactive/'}
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://repository.activeeon.com/content/groups/proactive/"
        }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.2.1'
        // Code Formatting dependencies
        classpath "com.diffplug.gradle.spotless:spotless:2.4.0"
        classpath "org.ow2.proactive:coding-rules:1.0.0"
        delete "gradle/ext"
        ant.unjar src: configurations.classpath.find { it.name.startsWith("coding-rules") }, dest: 'gradle/ext'
        // END code formatting dependencies
    }
}

configurations {
    provided
}

sourceSets {
    main {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
}

apply from: "$rootDir/gradle/ext/coding-format.gradle"

// Configure the release process plugin
release {
    // Add tags to the commits generated by this plugin. That way it is clear that no human did
    // those commits. That allows to not trigger automated builds with jenkins if those tags appear.
    preTagCommitMessage = '[ci skip][Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[ci skip][Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[ci skip][Gradle Release Plugin] - new version commit: '
}

dependencies {
    compile 'org.hibernate:hibernate-core:5.1.0.Final'
    compile 'org.jboss.logging:jboss-logging:3.3.0.Final'
    compile 'com.zaxxer:HikariCP:4.0.3'
}

// Upload the archives to the nexus repository. For execution, that needs to have
// the username and password set in the command line by -DnexusUsername=[username]
// and -DnexusPassword=[password]
uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = 'org.hibernate'

            snapshotRepository(url: "http://repository.activeeon.com/content/repositories/snapshots/") {
                authentication(userName: "${System.getProperty('nexusUsername')}",
                        password: "${System.getProperty('nexusPassword')}")
            }
            repository(url: "http://repository.activeeon.com/content/repositories/thirdparty/") {
                authentication(userName: "${System.getProperty('nexusUsername')}",
                        password: "${System.getProperty('nexusPassword')}")
            }
        }
    }
}
